runtime {
    options = ['--bind-services', '--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        jvmArgs = ["-Dawt.useSystemAAFontSettings=on", "-Dswing.aatext=true", "-Dsun.java2d.xrender=true"]
        noConsole = true
    }
    jpackage {
        def os = org.gradle.internal.os.OperatingSystem.current()
        installerOptions += [
                '--resource-dir', "src/main/resources",
                '--app-version', version
        ]
        mainJar = "${project.name}-${VERSION_NAME}.jar" // doesn't work with -SNAPSHOT versions
        mainClass = project.mainClassName
        imageOptions += ['--name', project.name]
        if (os.windows) {
            installerType = "msi"
            installerOptions += [
                    '--win-per-user-install', // TODO: because data is written into the install dir.
                    '--win-dir-chooser',
                    '--win-menu',
                    '--win-shortcut'
            ]
            imageOptions += [
                    '--icon', "icon/icon.ico",
                    '--win-console'
            ]
        } else if (os.macOsX) {
            installerType = "dmg"
            imageOptions += ['--mac-package-identifier', project.name]
            imageOptions += ['--icon', "icon/icon.icns"]
        } else {
            imageOptions += ['--icon', "icon/icon.png"]
        }
    }
}
