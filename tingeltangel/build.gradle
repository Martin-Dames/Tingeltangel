plugins {
    id 'application'
    id 'sablecc'
    id "org.beryx.runtime" version "1.12.1"
}

sourceCompatibility = "1.8"

group "$GROUP"
version "$VERSION_NAME"

mainClassName = "tingeltangel.Tingeltangel"

dependencies {

    implementation files('lib/AbsoluteLayout-SNAPSHOT.jar')

    implementation "javazoom:jlayer:1.0.1"
    implementation "org.yaml:snakeyaml:1.15"
    implementation "org.apache.logging.log4j:log4j-1.2-api:2.3"
    // required by the sablecc generated code.
    implementation 'javax.annotation:jsr305:1.0'

    testImplementation "junit:junit:$junitVersion"
}


jar.manifest {
    attributes("Main-Class": project.mainClassName)
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        jvmArgs = ["-Dawt.useSystemAAFontSettings=on", "-Dswing.aatext=true", "-Dsun.java2d.xrender=true"]
        noConsole = false
        arguments = ""
//        runInBinDir = true
    }
    jpackage {
        installerOptions += ['--resource-dir', "src/main/resources"]
        mainJar = "tingeltangel-0.8.0.jar"
        mainClass = "tingeltangel.Tingeltangel"
        // commandline argument for the jar file ?!? java -jar foo.jar XXXX ?
//        args = "gui-editor"
        def os = org.gradle.internal.os.OperatingSystem.current()
        if (os.windows) {
            installerType = "msi"
            imageOptions += ['--win-console']
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
    }
}
