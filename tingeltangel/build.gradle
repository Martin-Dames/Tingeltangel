plugins {
    id 'application'
    id 'sablecc'
    id "org.beryx.runtime" version "1.12.1"
}

sourceCompatibility = "1.8"

group "$GROUP"
version "$VERSION_NAME"

mainClassName = "tingeltangel.Tingeltangel"

dependencies {

    implementation files('lib/AbsoluteLayout-SNAPSHOT.jar')

    implementation "javazoom:jlayer:1.0.1"
    implementation "org.yaml:snakeyaml:1.15"
    implementation "org.apache.logging.log4j:log4j-1.2-api:2.3"
    // required by the sablecc generated code.
    implementation 'javax.annotation:jsr305:1.0'

    testImplementation "junit:junit:$junitVersion"
}


jar.manifest {
    attributes("Main-Class": project.mainClassName)
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    additive = true
    modules = ['java.management', 'java.naming']
    launcher {
        jvmArgs = ["-Dawt.useSystemAAFontSettings=on", "-Dswing.aatext=true", "-Dsun.java2d.xrender=true"]
        noConsole = false // true in the future
//        arguments = "" ???
    }
    jpackage {
        installerOptions += ['--resource-dir', "src/main/resources"] // completely missing in `jpackageImage`

        mainJar = "${project.name}-${VERSION_NAME}.jar" // doesn't work with -SNAPSHOT versions
        mainClass = project.mainClassName

        // commandline argument for the jar file ?!? like so: java -jar foo.jar XXXX ?
        // args = "gui-editor" // NOT working (https://badass-runtime-plugin.beryx.org/releases/latest/#_jpackage) wrong doc?
        def os = org.gradle.internal.os.OperatingSystem.current()
        if (os.windows) {
            installerType = "msi"
            imageOptions += ['--win-console']
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
    }
}
